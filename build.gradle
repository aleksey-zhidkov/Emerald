apply plugin: 'java'
apply plugin: 'kotlin'

final rcAnnotationsDir = 'annotations/rc-runtime'

version = '0.6.4'
group = 'lxx'

jar.archiveName = "${group}.${name}_${version}.jar"

buildscript {
    ext.kotlin_version = '1.0.6'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile files('lib/robocode-1.9.2.3.jar')
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    testCompile 'junit:junit:4.11'
}

task generateProperties << {

    def emeraldProperties = file("${sourceSets.main.output.classesDir}/lxx/${project.name}.properties")
    if (!emeraldProperties.parentFile.exists()) {
        emeraldProperties.parentFile.mkdirs()
    }
    def epWriter = emeraldProperties.newWriter()
    epWriter << "robot.description=Nothing interesting yet\n"
    epWriter << "robocode.version=1.9\n"
    epWriter << "robot.java.source.included=true\n"
    epWriter << "robot.version=${project.version}\n"
    epWriter << "robot.author.name=Aleksey Zhidkov\n"
    epWriter << "robot.classname=lxx.${project.name}\n"
    epWriter << "robot.name=${project.name}\n"
    epWriter.flush()
    epWriter.close()
}

task copyKotlinRuntime << {
    def configuration = project.configurations.getByName("compile")
    def allDependencies = configuration.getResolvedConfiguration().getResolvedArtifacts().collect() {
        return it.file
    }
    println(allDependencies)
    project.copy {
        from(zipTree(allDependencies[1]))
        from(zipTree(allDependencies[0]))
        into(sourceSets.main.output.classesDir)
    }
}

task copySources << {

    project.copy {
        from(sourceSets.main.java)
        into(sourceSets.main.output.classesDir)

        from(sourceSets.main.kotlin)
        into(sourceSets.main.output.classesDir)

    }
}

jar.dependsOn generateProperties
jar.dependsOn copySources
jar.dependsOn copyKotlinRuntime

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}


task wrapper(type: Wrapper) {
    gradleVersion = '2.14'
}